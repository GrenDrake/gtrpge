/* ************************************************************************* *
 * GOODS FOR SALE IN TOWN                                                    *
 * ************************************************************************* */

ITEM teleport-crystal {
    price 5;
    article "a ";
    name    "teleport crystal";
    plural  "teleport crystals";
    description "A crystal devised by Arnold of Va-Katr for the purpose of reaching the town quickly without the use of undue magic.";
    on-use {
        teleport-crystal 1 remove-items
        "Light flashes and you vision goes blank. Moments later, the world returns."
        say
        town-gate
        add-continue
    };
}

ITEM gnoll-potion {
    price 25;
    article "a ";
    name    "potion of gnoll";
    plural  "potions of gnoll";
    description "A potion of transformation intended to make one more gnoll-like.";
    on-use {
        gnoll-potion 1 remove-items
        add-return
        the-player get-species
        spc-gnoll already-gnoll jump-eq

        the-player spc-gnoll set-species
        >"You feel your body twist and change under the influence of the potion. Moments later, you have the body of a gnoll."
        end

        label already-gnoll
        >"Nothing happens. Perhaps you can't become any more gnoll-like then you are?"
    };
}

ITEM human-potion {
    price 20;
    article "a ";
    name    "potion of humanity";
    plural  "potions of humanity";
    description "A potion of transformation intended to make one more human-like.";
    on-use {
        human-potion 1 remove-items
        add-return
        the-player get-species
        spc-human already-human jump-eq

        the-player spc-human set-species
        >"You feel your body twist and change under the influence of the potion. Moments later, you have the body of a human."
        end

        label already-human
        >"Nothing happens. Perhaps you can't become any more human-like then you are?"
    };
}

ITEM common-sword {
    price 20;
    article "a ";
    name    "common sword";
    plural  "common swords";
    description "A perfectly ordinary sword.";
    slot    "weapon";
    can-equip {
        push true
    };
    action-list list(
        act-swing
        act-thrust
    );
}

ITEM common-helmet {
    price 15;
    article "a ";
    name    "common helmet";
    plural  "common helmets";
    slot    "head";
    description "A basic pot-helm.";
    resistances map(
        dt-blunt 5;
        dt-cutting 10;
    );
}

ITEM plate-armour {
    price 100;
    article "a ";
    name    "suit of plate armour";
    plural  "suits of plate armour";
    slot    "body";
    description "A suit of fine plate mail.";
    resistances map(
        dt-blunt 20;
        dt-cutting 35;
        dt-piercing 15;
    );
    skills map(
        skl-dexterity -3;
    );
}

/* ************************************************************************* *
 * TOWN LOCATIONS AND EVENTS                                                 *
 * ************************************************************************* */

SCENE in-town {
    location "In Town";
    body {
        >"You are inside the bazaar of the town; colourful stalls surround you and the sound of voices fills the air. The gate out of town is to the north while several stalls may be of interest. Particularly, there is a large tent with a sign reading \"Al the Alchemist Extraordinaire!\". Across the way from that is a strength-testing machine. Slightly further away, a blacksmith has set up their forge."
        "Visit alchemist's tent" town-alchemist add-option
        "Visit the blacksmith" town-blacksmith add-option
        "Test your strength" town-strength-tester add-option
        "Leave town" town-gate add-option
    };
}

SCENE town-strength-tester {
    /*
        The purpose of this scene is to demonstrate making out-of-combat skill checks
    */
    body {
        add-return

        0 party-at stk-dup say-uf
        >" picks up the hammer sitting next to the strength testing machine and slams it down on the target as hard "
        stk-dup pro-subject say-pronoun
        >" can! "
        skl-strength 0 25 skill-check
        0 failed jump-lt

        >"\nThe indicator shoots up, hitting the bell and making it ring loudly!"
        end
        LABEL failed
        >"\nThe indicator shoots up, but falls again before it reaches the bell."
    };
}

/* ************************************************************************* *
 * THE ALCHEMIST                                                             *
 * ************************************************************************* */

SCENE town-alchemist {
    /*
        Al the Alchemical Extraordinaire is an example of a very basic shop; Al
        offers no services beyond selling a few basic items.
    */
    location "Al the Alchemist Extraordinaire's Tent";
    body {
        >"You stand in an elaborately decorated tent with all manner of alchemical decor, such as bubbling flasks and vials of strangely coloured liquids, swatches of brightly coloured cloth, and cases of books with incomprehensible titles. An older man with an erect bearing beams at you from beside a counter."
        "Buy something" town-alchemist-talk add-option
        "Leave the tent" in-town add-option
    };
}

SCENE town-alchemist-talk {
    sale-list list(
        teleport-crystal
        gnoll-potion
        human-potion
    );
    body {
        >"\"Hello!\" he says. \"Would you like to buy something?\"\n"

        _9 town-alchemist-talk $sale-list get-property list-size store
                        // the number of items for sale
        _8 0 store      // current list position

        LABEL list-start
        *_8 *_9 list-done jump-gte

        _7 town-alchemist-talk $sale-list get-property *_8 list-get store
        *_7 say-uf
        >" is "
        *_7 $price get-property say-number
        >" gold pieces\n"

        gold item-qty *_7 $price get-property cant-afford jump-lt
        *_7 $name get-property town-alchemist-buy *_7 add-option-xtra
        LABEL cant-afford

        // go to next item
        _8 *_8 increment store
        list-start jump

        LABEL list-done

        "Buy nothing" 0 add-option
    };
}

SCENE town-alchemist-buy {
    body {
        town-alchemist-talk add-continue
        gold *_0 $price get-property remove-items
        cant-afford jump-false

        *_0 1 add-items
        >"You buy "
        *_0 say
        >"."
        end

        LABEL cant-afford
        >"You can't afford that!"
    };
}

/* ************************************************************************* *
 * THE BLACKSMITH                                                            *
 * ************************************************************************* */

SCENE town-blacksmith {
    /*
        The Blacksmith is a more complicated shop that the alchemist and sells
        a variety of goods that the player will want. In addition, the player
        can engage the blacksmith in conversation on a variety of topics.
    */
    location "The Town Blacksmith";
    body {
        >"The blacksmith is located in a simple, but well built and maintained forge. The blacksmith, a large, heavily muscled human, works tirelessly."
        "Talk to the blacksmith" town-blacksmith-talk add-option
        "Leave the tent" in-town add-option
    };
}

SCENE town-blacksmith-talk {
    body {
        >"Welcome, " 0 party-at get-species say >"! How may I help you?"
        "Buy something" town-blacksmith-store add-option
        "Ask about the town" town-blacksmith-abouttown add-option
        "Finish talking" 0 add-option
    };
}

SCENE town-blacksmith-abouttown {
    body {
        >"You ask the blacksmith about the town.\n\"I admit, it's not large, but it's what you can find here that counts and you won't find any town with as many shops at this one anywhere in this world!\""
        town-blacksmith-talk add-continue
    };
}

SCENE town-blacksmith-store {
    sale-list list(
        common-sword
        common-helmet
        plate-armour
    );
    body {
        >"\"What would you like?\"\n"

        _9 town-blacksmith-store $sale-list get-property list-size store
                        // the number of items for sale
        _8 0 store      // current list position

        LABEL list-start
        *_8 *_9 list-done jump-gte

        _7 town-blacksmith-store $sale-list get-property *_8 list-get store
        *_7 say-uf
        >" is "
        *_7 $price get-property say-number
        >" gold pieces\n"

        gold item-qty *_7 $price get-property cant-afford jump-lt
        *_7 $name get-property town-blacksmith-buy *_7 add-option-xtra
        LABEL cant-afford

        // go to next item
        _8 *_8 increment store
        list-start jump

        LABEL list-done

        "Buy nothing" 0 add-option
    };
}

SCENE town-blacksmith-buy {
    body {
        town-blacksmith-store add-continue
        gold *_0 $price get-property remove-items
        cant-afford jump-false

        *_0 1 add-items
        >"You buy "
        *_0 say
        >"."
        end

        LABEL cant-afford
        >"You can't afford that!"
    };
}