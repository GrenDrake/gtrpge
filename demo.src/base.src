TITLE "Forest of the Gnoll";
VERSION "Alpha-1";
BYLINE "Gren Drake, 2017";

// constants that are used to refer to storage locations
CONSTANT flg-took-key 0;
CONSTANT flg-rescued-gnoll 1;
CONSTANT flg-talked-to-caged-gnoll 2;
CONSTANT flg-visited-gnoll 3;

CONSTANT forest-events 77;

/* ************************************************************************* *
 * GENERAL DEFINITIONS                                                       *
 * ************************************************************************* */

// constants for species flag definitions
CONSTANT sflg-biped 0x01;
CONSTANT sflg-quad  0x02;
CONSTANT sflg-snout 0x04;

SPECIES spc-human {
    "human" ( sflg-biped )
}

SPECIES spc-gnoll {
    "gnoll" ( sflg-biped sflg-snout )
}

SPECIES spc-dragon {
    "dragon" ( sflg-quad sflg-snout )
}

SEX sex-male {
    "male" ( /* sflg-male */ )
    "he"      // subject  : _he_ ate dinner
    "him"     // object   : dinner ate _him_
    "his"     // possess  : the dinner was _his_
    "his"     // adject   : it's _his_ dinner
    "himself" // reflex   : he ate _himself_
}

SEX sex-female {
    "female"
    "she" "her" "hers" "her" "herself"
}

SEX sex-neuter {
    "neuter"
    "it" "it" "its" "its" "itself"
}

CONSTANT skl-variable       0x01; // variable stat (like health, energy, mana, corruption, etc.)
CONSTANT skl-ko-0           0x02; // KO character if reaches 0
CONSTANT skl-ko-full        0x04; // KO character is becomes full
CONSTANT skl-reset-on-rest  0x08; // current skill value is reset when character rests
CONSTANT skl-x5             0x10; // multiply (max) value of skill by 5 for final value

//    iname             stat            dname            default-value  flags
SKILL skl-strength      0               "strength"       10;
SKILL skl-dexterity     0               "dexterity"      10;
SKILL skl-toughness     0               "toughness"      10; // consitution
SKILL skl-knowledge     0               "knowledge"      10;
SKILL skl-spirit        0               "spirit"         10; // wisdom
SKILL skl-unarmed       skl-strength    "unarmed combat" 0;
SKILL skl-melee         skl-strength    "melee combat"   0;
SKILL skl-ranged        skl-dexterity   "ranged combat"  0;
SKILL skl-thievery      skl-dexterity   "thievery"       0;
SKILL skl-persuasion    skl-spirit      "persuasion"     0;
SKILL skl-black-magic   skl-knowledge   "black magic"    0;
SKILL skl-holy-magic    skl-knowledge   "holy magic"     0;
SKILL skl-void-magic    skl-knowledge   "void magic"     0;
SKILL skl-health        0               "health"         5   ( skl-variable skl-ko-0 skl-reset-on-rest skl-x5 );
SKILL skl-energy        0               "energy"         2   ( skl-variable skl-reset-on-rest skl-x5 );
SKILL skl-void          0               "void-touched"   0   ( skl-variable skl-ko-full );

/* ************************************************************************* *
 * GENERAL CHARACTER DEFINITIONS                                             *
 * ************************************************************************* */

CHARACTER the-player {
    "the " "player"
    sex-neuter spc-human
    faction 0
    skills (
        skl-strength 12;
        skl-toughness 14;
        skl-melee 4;
    )
}

/* ************************************************************************* *
 * GENERAL ITEM DEFINITIONS                                                  *
 * ************************************************************************* */

ITEM common-sword {
    "a " "common sword" "common swords"
    slot "weapon"
    canEquip {
        push true;
    }
    onEquip {
    }
    onRemove {
    }
}

ITEM teleport-crystal {
    "a " "teleport crystal" "teleport crystals"
    onUse {
        remove-items 1 teleport-crystal;
        say "Light flashes and you vision goes blank. Moments later, the world returns.";
        add-continue town-gate;
    }
}

ITEM gnoll-potion {
    "a " "potion of gnoll" "potions of gnoll"
    onUse {
        remove-items 1 gnoll-potion;
        add-return;
        get-species the-player;
        jump-eq spc-gnoll already-gnoll;

        set-species the-player spc-gnoll;
        say "You feel your body twist and change under the influence of the potion. Moments later, you have the body of a gnoll.";
        end;

        label already-gnoll;
        say "Nothing happens. Perhaps you can't become any more gnoll-like then you are?";
    }
}

/* ************************************************************************* *
 * BASE GAME NODES                                                           *
 * ************************************************************************* */

NODE start {
    say "Welcome to the game!";
    add-option "Create a new character" chargen-sex;

    do-node forest-event-setup;
}

NODE chargen-sex {
    say "What sex is your character?";
    add-option-xtra "Male" chargen-species sex-male;
    add-option-xtra "Female" chargen-species sex-female;
}

NODE chargen-species {
    set-sex the-player stack; // set player sex
    say "What species is your character?";
    add-option-xtra "Human" chargen-done spc-human;
    add-option-xtra "Gnoll" chargen-done spc-gnoll;
    add-option-xtra "Dragon" chargen-done spc-dragon;
}

NODE chargen-done {
    set-species the-player stack; // set player species
    add-to-party the-player;

    add-items 20 teleport-crystal;
    add-items 2 gnoll-potion;

    say "Beginning the game now.\n* * * * * * * * * *\n";
    start-game;
    do-node forest-path;
}
