TITLE "Forest of the Gnoll";
VERSION "Alpha-1";
BYLINE "Gren Drake, 2017";

// constants that are used to refer to storage locations
CONSTANT flg-took-key 0;
CONSTANT flg-rescued-gnoll 1;
CONSTANT flg-talked-to-caged-gnoll 2;
CONSTANT flg-visited-gnoll 3;

CONSTANT forest-events 77;
CONSTANT player 99;

/* ************************************************************************* *
 * GENERAL DEFINITIONS                                                       *
 * ************************************************************************* */

// constants for species flag definitions
CONSTANT sflg-biped 0x01;
CONSTANT sflg-quad  0x02;
CONSTANT sflg-snout 0x04;

SPECIES spc-human {
    "human" ( sflg-biped )
}

SPECIES spc-gnoll {
    "gnoll" ( sflg-biped sflg-snout )
}

SPECIES spc-dragon {
    "dragon" ( sflg-quad sflg-snout )
}

SEX sex-male {
    "male" ( /* sflg-male */ )
    "he"      // subject  : _he_ ate dinner
    "him"     // object   : dinner ate _him_
    "his"     // possess  : the dinner was _his_
    "his"     // adject   : it's _his_ dinner
    "himself" // reflex   : he ate _himself_
}

SEX sex-female {
    "female"
    "she" "her" "hers" "her" "herself"
}

SEX sex-neuter {
    "neuter"
    "it" "it" "its" "its" "itself"
}

//    iname             stat            dname           default-value
SKILL skl-strength      0               "strength"      10;
SKILL skl-dexterity     0               "dexterity"     10;
SKILL skl-toughness     0               "toughness"     10; // consitution
SKILL skl-intelligence  0               "intelligence"  10;
SKILL skl-spirit        0               "spirit"        10; // wisdom
SKILL skl-one-handed    skl-dexterity   "one handed"    0;
SKILL skl-two-handed    skl-dexterity   "two handed"    0;
SKILL skl-unarmed       skl-dexterity   "unarmed"       0;

/* ************************************************************************* *
 * GENERAL CHARACTER DEFINITIONS                                             *
 * ************************************************************************* */

CHARACTER the-player {
    "the " "player"
    sex-neuter spc-human
    faction 0
}

/* ************************************************************************* *
 * GENERAL ITEM DEFINITIONS                                                  *
 * ************************************************************************* */

ITEM common-sword {
    "a " "common sword" "common swords"
}

ITEM teleport-crystal {
    "a " "teleport crystal" "teleport crystals"
    onUse {
        remove-items 1 teleport-crystal;
        say "Light flashes and you vision goes blank. Moments later, the world returns.";
        add-continue town-gate;
    }
}

ITEM gnoll-potion {
    "a " "potion of gnoll" "potions of gnoll"
}

/* ************************************************************************* *
 * BASE GAME NODES                                                           *
 * ************************************************************************* */

NODE start {
    say "Welcome to the game!";
    add-option-xtra "Begin Game (male)" do-chargen "male";
    add-option-xtra "Begin Game (female)" do-chargen "female";

    do-node forest-event-setup;
}

NODE do-chargen {
    make-character the-player;
    store player stack;

    push #xtra;
    jump-eq "male" do-male;
    set-sex #player sex-female;
    jump sex-set;
    label do-male;
    set-sex #player sex-male;
    label sex-set;

    add-items 20 teleport-crystal;
    do-node forest-path;
}
