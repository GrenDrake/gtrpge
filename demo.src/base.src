CONSTANT title "Forest of the Gnoll";
CONSTANT version "Alpha-1";
CONSTANT byline "Gren Drake, 2017";

// constants that are used to refer to storage locations
CONSTANT flg-took-key 0;
CONSTANT flg-rescued-gnoll 1;
CONSTANT flg-talked-to-caged-gnoll 2;
CONSTANT flg-visited-gnoll 3;

CONSTANT forest-events 77;

/* ************************************************************************* *
 * GENERAL DEFINITIONS                                                       *
 * ************************************************************************* */

// constants for species flag definitions
SPECIES spc-human {
    name "human";
    is-biped true;
}

SPECIES spc-gnoll {
    name "gnoll";
    is-biped true;
    has-snout true;
}

SPECIES spc-dragon {
    name "dragon";
    is-quad true;
    has-snout true;
}

SEX sex-male {
    name "male";
    subject "he";        // subject  : _he_ ate dinner
    object "him";        // object   : dinner ate _him_
    possessive "his";    // possess  : the dinner was _his_
    adjective "his";     // adject   : it's _his_ dinner
    reflexive "himself"; // reflex   : he ate _himself_
}

SEX sex-female {
    name "female";
    subject "she";
    object "her";
    possessive "hers";
    adjective "her";
    reflexive "herself";
}

SEX sex-neuter {
    name "neuter";
    subject "it";
    object "it";
    possessive "its";
    adjective "its";
    reflexive "itself";
}

CONSTANT skl-variable       0x01; // variable stat (like health, energy, mana, corruption, etc.)
CONSTANT skl-ko-0           0x02; // KO character if reaches 0
CONSTANT skl-ko-full        0x04; // KO character is becomes full
CONSTANT skl-reset-on-rest  0x08; // current skill value is reset when character rests
CONSTANT skl-x5             0x10; // multiply (max) value of skill by 5 for final value
CONSTANT skl-on-tracker     0x20; // display skill on combat tracker

//    iname             stat            dname            default-value  flags
SKILL skl-strength      0               "strength"       10;
SKILL skl-dexterity     0               "dexterity"      10;
SKILL skl-toughness     0               "toughness"      10; // consitution
SKILL skl-knowledge     0               "knowledge"      10;
SKILL skl-spirit        0               "spirit"         10; // wisdom
SKILL skl-unarmed       skl-strength    "unarmed combat" 0;
SKILL skl-melee         skl-strength    "melee combat"   0;
SKILL skl-ranged        skl-dexterity   "ranged combat"  0;
SKILL skl-thievery      skl-dexterity   "thievery"       0;
SKILL skl-persuasion    skl-spirit      "persuasion"     0;
SKILL skl-black-magic   skl-knowledge   "black magic"    0;
SKILL skl-holy-magic    skl-knowledge   "holy magic"     0;
SKILL skl-void-magic    skl-knowledge   "void magic"     0;
SKILL skl-health        0               "health"         5   ( skl-variable skl-ko-0 skl-reset-on-rest skl-x5 skl-on-tracker );
SKILL skl-energy        0               "energy"         2   ( skl-variable skl-reset-on-rest skl-x5 skl-on-tracker );
SKILL skl-void          0               "void-touched"   10   ( skl-variable skl-ko-full skl-on-tracker );

DAMAGE-TYPES {
    dt-blunt   "blunt"
    dt-cutting "cutting"
    dt-piercing "piercing"
    dt-fire "fire"
    dt-air "air"
    dt-earth "earth"
    dt-water "water"
    dt-void "void"
}

/* ************************************************************************* *
 * GENERAL CHARACTER DEFINITIONS                                             *
 * ************************************************************************* */

CHARACTER the-player {
    article "the ";
    name "player";
    sex sex-neuter;
    species spc-human;
    faction 0;
    skills skillset(
        defaults;
        skl-strength 12;
        skl-toughness 14;
        skl-melee 4;
    );
    gear list(
        common-helmet
        common-sword
        loincloth
    );
    baseAbilities list(
        act-punch
    );
    extraAbilities list(
        act-fireball
    );
}

/* ************************************************************************* *
 * GENERAL ITEM DEFINITIONS                                                  *
 * ************************************************************************* */

ITEM common-sword {
    article "a ";
    name    "common sword";
    plural  "common swords";
    description "A perfectly ordinary sword.";
    slot    "weapon";
    can-equip {
        push true;
    };
    on-equip {
    };
    on-remove {
    };
    action-list list(
        act-swing
        act-thrust
    );
}

ITEM common-helmet {
    article "a ";
    name    "common helmet";
    plural  "common helmets";
    slot    "head";
}
ITEM loincloth {
    article "a ";
    name    "loincloth";
    plural  "loincloths";
    slot    "body";
}
ITEM plate-armour {
    article "a ";
    name    "suit of plate armour";
    plural  "suits of plate armour";
    slot    "body";
    skills skillset(
        skl-strength 50;
        skl-dexterity -3;
    );
}

ITEM teleport-crystal {
    article "a ";
    name    "teleport crystal";
    plural  "teleport crystals";
    description "A crystal devised by Arnold of Va-Katr for the purpose of reaching the town quickly without the use of undue magic.";
    on-use {
        remove-items 1 teleport-crystal;
        say "Light flashes and you vision goes blank. Moments later, the world returns.";
        add-continue town-gate;
    };
}

ITEM gnoll-potion {
    article "a ";
    name    "potion of gnoll";
    plural  "potions of gnoll";
    description "A potion of transformation intended to make one more gnoll-like.";
    on-use {
        remove-items 1 gnoll-potion;
        add-return;
        get-species the-player;
        jump-eq spc-gnoll already-gnoll;

        set-species the-player spc-gnoll;
        say "You feel your body twist and change under the influence of the potion. Moments later, you have the body of a gnoll.";
        end;

        label already-gnoll;
        say "Nothing happens. Perhaps you can't become any more gnoll-like then you are?";
    };
}

/* ************************************************************************* *
 * GENERAL SKILL DEFINITIONS                                                  *
 * ************************************************************************* */

ACTION act-punch {
    name "punch";
    combat-node { };
}

ACTION act-fireball {
    name "fireball";
    cost-skill skl-energy;
    cost-amount 2;
    combat-node {
        say-uf #_0;
        say " uses fireball! KABOOM!\n";
    };
}

ACTION act-swing {
    name "swing";
    combat-node {
        say-uf #_0;
        say " swings ";
        say-pronoun #_0 pro-adject;
        say " sword! ";


        store _9 0;
        combatant 0;
        store _8 stack;
        label swing-start;

        fetch _8;
        fetch _0;
        subtract;
        jump-false skip-me;

        // skill-check cRef skillRef Mods Target

        skill-check #_0 skl-melee 0 25;
        jump-gt 0 hit-missed;

        say-uf #_8;
        say " is hit! ";
        do-damage #_8 13 skl-health dt-cutting;
        jump skip-me;

        label hit-missed;
        say-uf #_8;
        say " avoids the strike! ";

        label skip-me;

        fetch _9;
        push 1;
        add;
        store _9 stack;
        combatant #_9;
        store _8 stack;
        fetch _8;
        jump-true swing-start;

        say "\n";
    };
    peace-node {
        say "You brandish the sword wildly.";
        add-return;
    };
}

ACTION act-thrust {
    name "thrust";
    peace-node {
        say "You brandish the sword wildly.";
        add-return;
    };
}

/* ************************************************************************* *
 * BASE GAME NODES                                                           *
 * ************************************************************************* */

NODE start {
    "Welcome to the game!";
    add-option "Create a new character" chargen-sex;

    do-node forest-event-setup;
}

NODE chargen-sex {
    say "What sex is your character?";
    add-option-xtra "Male" chargen-species sex-male;
    add-option-xtra "Female" chargen-species sex-female;
}

NODE chargen-species {
    set-sex the-player stack; // set player sex
    say "What species is your character?";
    add-option-xtra "Human" chargen-done spc-human;
    add-option-xtra "Gnoll" chargen-done spc-gnoll;
    add-option-xtra "Dragon" chargen-done spc-dragon;
}

NODE chargen-done {
    set-species the-player stack; // set player species
    add-to-party the-player;
    add-to-party the-gnoll;

    add-items 20 teleport-crystal;
    add-items 2 gnoll-potion;
    add-items 1 loincloth;
    add-items 1 plate-armour;

    say "Beginning the game now.\n* * * * * * * * * *\n";
    start-game;
    do-node forest-path;
}
