/* ************************************************************************* *
 * FOREST ITEMS                                                              *
 * ************************************************************************* */

ITEM gnoll-cage-key {
    article "a ";
    name    "large key";
    plural "";
}

/* ************************************************************************* *
 * FOREST CHARACTERS                                                         *
 * ************************************************************************* */

CHARACTER the-gnoll {
    article "the ";
    name "gnoll";
    sex sex-male;
    species spc-gnoll;
    faction 0;
    skills map(
        skl-strength 12;
        skl-toughness 14;
        skl-void-magic 4;
    );
    base-abilities list(
        act-punch
    );
    extra-abilities list(
    );
    gear list(
        common-sword
    );
}

CHARACTER imp-1 {
    article "a ";
    name "tall male imp";
    sex sex-male;
    species spc-human;
    faction 1;
    ai {
        // 1-in-3 chance of performing a whirlwind attack
        0 2 random do-whirl jump-false

        *_0 $faction get-property random-not-faction
        *_0 2 act-thrust $combat-node get-property call
        end

        LABEL do-whirl
        *_0 1
        act-swing $combat-node get-property call
    };
}
CHARACTER imp-2 {
    article "a ";
    name "fat male imp";
    sex sex-male;
    species spc-human;
    faction 1;
    skills map(
        skl-melee 4;
    );
    ai {
        *_0 1 imp-1 $ai get-property call
    };
}
CHARACTER imp-3 {
    article "a ";
    name "female imp";
    sex sex-female;
    species spc-human;
    faction 1;
    ai {
        *_0 1 imp-1 $ai get-property call
    };
}

/* ************************************************************************* *
 * FOREST NODES                                                              *
 * ************************************************************************* */

SCENE forest-path {
    location "On Forest Path";
    body {
        >"You are on a path leading through a forest. The crumbling remains of a stone wall can be seen on one side, the words \"TOWN\" and a southward arrow has been painted on the wall. The wall partially hides an overgrown side trail."
        "Go north into the forest" forest-fork  add-option
        "Go south to town"         town-gate    add-option
        "Take the overgrown trail" gnoll-cage   add-option
        "Rest Awhile"              rest-awhile  add-option
    };
}

SCENE town-gate {
    location "Outside the Town Gates";
    body {
        >"You stand before a gate built into the large, wooden pallisade that surronds the town. The gate is open, ready to admit you to the market bzzaar within."
        "Head into town" in-town add-option
        "Return to the forest" forest-path add-option
    };
}

SCENE gnoll-cage {
    location "By a Cage in the Forest";
    body {
        "You stand in a clearing in the forest. The area is dominated by a large, if rusty, cage."
        say
        push "Return to the forest path"
        push forest-path
        add-option

        flg-rescued-gnoll fetch
        push no-gnoll
        jump-true
        " Inside the cage you can see a rather emaciated gnoll."
        say
        push "Open the cage"
        push open-cage
        add-option
        push "Try to talk to the gnoll"
        push talk-to-gnoll
        add-option
        end

        label no-gnoll
        " The cage stands empty and open."
        say
    };
}

SCENE talk-to-gnoll {
    body {
        add-return
        flg-talked-to-caged-gnoll fetch
        push second-talk
        jump-true
        flg-talked-to-caged-gnoll true store
        "You can barely make out the gnoll's words; \"Help me, please! The imps, they have the key.\""
        say
        end
        label second-talk
        "The gnoll continues to plead for help in a barely audible voice. \"The imps...\""
        say
    };
}

SCENE open-cage {
    body {
        add-return
        gnoll-cage-key item-qty
        push no-key
        jump-false
        "Using the key you found you are able to open the cage; as you do, both the key and the lock turn to ash. As you open the door, the gnoll rushes out surprisingly fast given his state.\n\n\"Thank you!\" he says. \"Please, come see me in my home. I live to the north of the town.\""
        say
        gnoll-cage-key 1 remove-items
        flg-rescued-gnoll true store
        end

        label no-key
        "You rattle the door of the cage, but it is firmly locked."
        say
    };
}

SCENE forest-fork {
    location "A Fork in the Path";
    body {
        >"The forest path branches here. One fork leads northward, a faded sign labelling it as \"GNOLL HOME\". A northeast fork deeper into the forest, suitable for exploring what hides in the forest's shadowed reaches. To the south, the path leads back towards town. A western trail loop arounds to a discreet clearing hidden by dense brush."

        "Take the north path"           outside-cabin       add-option
        "Explore the forest"            forest-do-event     add-option
        "Check the discreet clearing"   discreet-clearing   add-option
        "Return to the south"           forest-path         add-option
    };
}

SCENE outside-cabin {
    location "A Cabin in the Forest";
    body {
        "A small cabin has been built here, nestled between the trees."
        say
        flg-rescued-gnoll fetch
        push no-gnoll
        jump-false
        " A trail of smoke can be seen curling up from the chimney; it appears someone is home!"
        say
        label no-gnoll
        push "Re-eneter the forest"
        push forest-fork
        add-option
        push "Try to enter the cabin."
        push entering-cabin
        add-option
    };
}


/* ************************************************************************* *
 * VILLANOUS IMPS                                                            *
 * ************************************************************************* */

CONSTANT defeated-imps 97;

SCENE discreet-clearing {
    location "A Discreet Clearing";
    body {
        *defeated-imps imps-done jump-true
        >"Three imps are gathered around a campfire, cackling and conversing. As you enter the clearing, they look up at you.\n\"An intruder! Get him!\""
        main-imp-fight-after reset-combat
        imp-1 add-to-combat
        imp-2 add-to-combat
        imp-3 add-to-combat
        end

        LABEL imps-done
        >"This is a quiet clearing screen from view from the path by the brush. The remains of a campfire are visible in the centre. No one else is here; the only thing you can do is leave."
        "Leave the clearing" forest-fork add-option
    };
}

SCENE main-imp-fight-after {
    body {
        combat-status 0 player-defeat jump-lt

        LABEL player-victory
        defeated-imps true store
        >"The imps lie unconscious around you, defeated. Scattered around the area, you find 200 gold and a large key.\n"
        gold 200 add-items
        gnoll-cage-key 1 add-items
        add-return
        end

        LABEL player-defeat
        >"You fall, defeated. You awaken some time later back in the forest.\n"
        forest-fork add-continue
    };
}
/* ************************************************************************* *
 * THE GNOLL'S CABIN                                                         *
 * ************************************************************************* */

SCENE entering-cabin {
    body {
        flg-rescued-gnoll fetch
        push  gnoll-home
        jump-true
        "Try as you might, the door is quite secure."
        say
        add-return
        end

        label gnoll-home
        flg-visited-gnoll true store
        "You hear the voice of the gnoll call out from inside. \"Come on in!\"\n\n"
        say
        0 inside-cabin call
    };
}

SCENE inside-cabin {
    location "Inside the Gnoll's Cabin";
    body {
        "The interior of the cabin is crampt, but cozy. You can see the gnoll slotched back on a wooden chair, a mug of some dark liquid in his hand.\n\n\"Good to see you again, my rescuer!\" he says as you enter."
        say
        push "Talk to the gnoll"
        push gnoll-cabin-talk
        add-option
        push "Leave"
        push outside-cabin
        add-option
    };
}

SCENE gnoll-cabin-talk {
    body {
        "You have a nice chat with the gnoll." say
        add-return
    };
}


/* ************************************************************************* *
 * SOME RANDOM FOREST EVENTS                                                 *
 * ************************************************************************* */

OBJECT forest-event-data {
    events list(
        forest-event-skeleton
        forest-event-nothing
    );
}

SCENE forest-do-event {
    body {
        0
        forest-event-data $events get-property random-event
        call
    };
}

EVENT forest-event-skeleton {
    count { 1 };
    body {
        >"You discover a menacing looking skeleton loitering around a grave. It stares at you with empty eye-sockets for a moment then charges with a soundless cry."
        forest-event-skeleton-after reset-combat
        enemy-skeleton add-to-combat
    };
}
CHARACTER enemy-skeleton {
    article "a ";
    name "long-dead skeleton";
    sex sex-male;
    species spc-human;
    faction 1;
    skills map(
    );
    ai {
        *_0 $faction get-property random-not-faction
        *_0 2 act-thrust $combat-node get-property call
        end
    };
}
SCENE forest-event-skeleton-after {
    body {
        add-return
        combat-status 0 player-defeat jump-lt

        LABEL player-victory
        _5 0 20 random store
        gold *_5 add-items
        >"You have won the battle!"
        *_5 no-reward jump-false
        >" You found " *_5 say-number >" gold pieces!"
        LABEL no-reward
        >"\n"
        end

        LABEL player-defeat
        >"You have been defeated and wake some time later alone in the forest.\n"
    };
}


EVENT forest-event-nothing {
    count { 1 };
    body {
        "You stumble around in the forest for a while, but eventually end up back were you started without finding anything."
        say
        add-return
    };
}
