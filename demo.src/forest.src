/* ************************************************************************* *
 * FOREST ITEMS                                                              *
 * ************************************************************************* */

ITEM gnoll-cage-key {
    article "a ";
    name    "very shiny key";
    plural "";
    on-use {
        say "You admire the key for a few minutes.";
        add-return;
    };
}

/* ************************************************************************* *
 * FOREST CHARACTERS                                                         *
 * ************************************************************************* */

CHARACTER the-gnoll {
    "the " "gnoll"
    sex-male spc-gnoll
    ( /* cflg-character */ )
    faction 0
    skills (
        skl-strength 12;
        skl-toughness 14;
        skl-void-magic 4;
    )
    baseAbilities (
        act-punch
    )
    gear (
        common-sword
    )
}

CHARACTER imp-1 {
    "a " "tall male imp"
    sex-male spc-human
    ()
    faction 1
}
CHARACTER imp-2 {
    "a " "fat male imp"
    sex-male spc-human
    ()
    faction 1
}
CHARACTER imp-3 {
    "a " "female imp"
    sex-female spc-human
    ()
    faction 1
}

/* ************************************************************************* *
 * FOREST NODES                                                              *
 * ************************************************************************* */

NODE forest-path {
    set-location "On Forest Path";
    say "You are on a path leading through a forest. The crumbling remains of a stone wall can be seen on one side, partially hiding an overgrown side trail.";
    add-option "Follow the path north" forest-fork;
    add-option "Follow the path south" town-gate;
    add-option "Examine the stone wall" examine-wall;
    add-option "Take the overgrown trail" gnoll-cage;
    add-option "Hurt yourself" hurt-self-health;
    add-option "Inflict void on yourself" hurt-self-void;
    add-option "Fight some immps" imp-fight;
}

NODE hurt-self-health {
    do-damage 0 56 skl-health dt-blunt;
    say "You hurt yourself.";
    add-return;
}

NODE hurt-self-void {
    do-damage the-player -6 skl-void dt-blunt;
    say "You hurt yourself.";
    add-return;
}

NODE examine-wall {
    say "It looks much as one might expect.";
    add-time 1 12;
    do-damage the-player 5 skl-health dt-blunt;
    add-return;
}

NODE imp-fight {
    say "Three imps are gathered around a campfire, cackling and conversing.";
    reset-combat;
    add-to-combat imp-1;
    add-to-combat imp-2;
    add-to-combat imp-3;
}


NODE town-gate {
    set-location "Outside the Town Gates";
    say "You stand before a gate built into the large, wooden pallisade that surronds the town. The gate, alas, is closed.";
    add-option "Return to the forest" forest-path;

    fetch flg-took-key;
    jump-true no-key;
    say "\n\nA key can be seen glinting in the sun not far from the gate.";
    add-option "Take key" take-key;
    label no-key;
}

NODE take-key {
    add-items 1 gnoll-cage-key;
    store flg-took-key true;
    say "You pick up the key and stick it in your pocket.";
    add-return;
}

NODE gnoll-cage {
    set-location "By a Cage in the Forest";
    say "You stand in a clearing in the forest. The area is dominated by a large, if rusty, cage.";
    add-option "Return to the forest path" forest-path;

    fetch flg-rescued-gnoll;
    jump-true no-gnoll;
    say " Inside the cage you can see a rather emaciated gnoll.";
    add-option "Open the cage" open-cage;
    add-option "Try to talk to the gnoll" talk-to-gnoll;
    end;

    label no-gnoll;
    say " The cage stands empty and open.";
}

NODE talk-to-gnoll {
    add-return;
    fetch flg-talked-to-caged-gnoll;
    jump-true second-talk;
    store flg-talked-to-caged-gnoll true;
    say "You can barely make out the gnoll's words; \"Help me, please!\"";
    end;
    label second-talk;
    say "The gnoll continues to plead for help in a barely audible voice.";
}

NODE open-cage {
    add-return;
    item-qty gnoll-cage-key;
    jump-false no-key;
    say "Using the key you found you are able to open the cage; as you do, both the key and the lock turn to ash. As you open the door, the gnoll rushes out surprisingly fast given his state.\n\n\"Thank you!\" he says. \"Please, come see me in my home. I live to the north of the town.\"";
    remove-items 1 gnoll-cage-key;
    store flg-rescued-gnoll true;
    end;

    label no-key;
    say "You rattle the door of the cage, but it is firmly locked.";
}

NODE forest-fork {
    set-location "A Fork in the Path";
    say "The path through the forest branches here, one fork leading north, towards what appears to be a small cabin while another leads deeper into the forest. It is also possible to return to the south, towards the town.";
    add-option "Take the north path" outside-cabin;
    add-option "Go deeper into the forest" deep-forest;
    add-option "Return to the south" forest-path;
}

NODE outside-cabin {
    set-location "A Cabin in the Forest";
    say "A small cabin has been built here, nestled between the trees.";
    fetch flg-rescued-gnoll;
    jump-false no-gnoll;
    say " A trail of smoke can be seen curling up from the chimney; it appears someone is home!";
    label no-gnoll;
    add-option "Re-eneter the forest" forest-fork;
    add-option "Try to enter the cabin." entering-cabin;
}

NODE entering-cabin {
    fetch flg-rescued-gnoll;
    jump-true gnoll-home;
    say "Try as you might, the door is quite secure.";
    add-return;
    end;

    label gnoll-home;
    store flg-visited-gnoll true;
    say "You hear the voice of the gnoll call out from inside. \"Come on in!\"\n\n";
    do-node inside-cabin;
}

NODE inside-cabin {
    set-location "Inside the Gnoll's Cabin";
    say "The interior of the cabin is crampt, but cozy. You can see the gnoll slotched back on a wooden chair, a mug of some dark liquid in his hand.\n\n\"Good to see you again, my rescuer!\" he says as you enter.";
    add-option "Talk to the gnoll" gnoll-cabin-talk;
    add-option "Leave" outside-cabin;
}

NODE gnoll-cabin-talk {
    say "You have a nice chat with the gnoll.";
    add-return;
}

NODE deep-forest {
    set-location "Deep in the Forest";
    say "You are deep within the forest. A path leads back out towards civilization, or you can take your chances exploring the forest.";
    add-option "Head towards civilization" forest-fork;
    add-option "Explore a while" forest-do-event;
}

/* ************************************************************************* *
 * SOME RANDOM FOREST EVENTS                                                 *
 * ************************************************************************* */

NODE forest-do-event {
    random-from-list #forest-events;
    do-node stack;
}
NODE forest-event-setup {
    create-list forest-events;
    add-to-list forest-event-imps #forest-events;
    add-to-list forest-event-nothing #forest-events;
    add-to-list forest-event-skeleton #forest-events;
}

NODE forest-event-imps {
    say "You stumble across a pair of imps eating a meal. They wish you a good day and you carry on your way.";
    add-return;
}

NODE forest-event-skeleton {
    say "You discover a menacing looking skeleton loitering around a grave. It fails to notice you entirely.";
    add-return;
}

NODE forest-event-nothing {
    say "You stumble around in the forest for a while, but eventually end up back were you started without finding anything.";
    add-return;
}