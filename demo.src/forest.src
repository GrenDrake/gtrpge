/* ************************************************************************* *
 * FOREST ITEMS                                                              *
 * ************************************************************************* */

ITEM gnoll-cage-key {
    article "a ";
    name    "very shiny key";
    plural "";
    on-use {
        say "You admire the key for a few minutes.";
        add-return;
    };
}

/* ************************************************************************* *
 * FOREST CHARACTERS                                                         *
 * ************************************************************************* */

CHARACTER the-gnoll {
    article "the ";
    name "gnoll";
    sex sex-male;
    species spc-gnoll;
    faction 0;
    skills skillset(
        defaults;
        skl-strength 12;
        skl-toughness 14;
        skl-void-magic 4;
    );
    baseAbilities list(
        act-punch
    );
    gear list(
        common-sword
    );
}

CHARACTER imp-1 {
    article "a ";
    name "tall male imp";
    sex sex-male;
    species spc-human;
    faction 1;
    skills skillset(
        defaults;
    );
    ai {
        say-uf #_0;
        " sings \"Dance magic, dance!\"\n";
    };
}
CHARACTER imp-2 {
    article "a ";
    name "fat male imp";
    sex sex-male;
    species spc-human;
    faction 1;
    skills skillset(
        defaults;
    );
}
CHARACTER imp-3 {
    article "a ";
    name "female imp";
    sex sex-male;
    species spc-human;
    faction 1;
    skills skillset(
        defaults;
    );
}

/* ************************************************************************* *
 * FOREST NODES                                                              *
 * ************************************************************************* */

NODE forest-path {
    push "On Forest Path";
    set-location;
    say "You are on a path leading through a forest. The crumbling remains of a stone wall can be seen on one side, partially hiding an overgrown side trail.";
    push "Follow the path north";
    push forest-fork;
    add-option;
    push "Follow the path south";
    push town-gate;
    add-option;
    push "Examine the stone wall";
    push examine-wall;
    add-option;
    push "Take the overgrown trail";
    push gnoll-cage;
    add-option;
    push "Hurt yourself";
    push hurt-self-health;
    add-option;
    push "Inflict void on yourself";
    push hurt-self-void;
    add-option;
    push "Fight some immps";
    push imp-fight;
    add-option;
}

NODE hurt-self-health {
    do-damage 0 56 skl-health dt-blunt;
    say "You hurt yourself.";
    add-return;
}

NODE hurt-self-void {
    do-damage the-player -6 skl-void dt-blunt;
    say "You hurt yourself.";
    add-return;
}

NODE examine-wall {
    say "It looks much as one might expect.";
    push 1;
    push 12;
    add-time;
    do-damage the-player 5 skl-health dt-blunt;
    add-return;
}

NODE imp-fight {
    say "Three imps are gathered around a campfire, cackling and conversing.";
    reset-combat;
    add-to-combat imp-1;
    add-to-combat imp-2;
    add-to-combat imp-3;
}


NODE town-gate {
    push "Outside the Town Gates";
    set-location;
    say "You stand before a gate built into the large, wooden pallisade that surronds the town. The gate, alas, is closed.";
    push "Return to the forest";
    push forest-path;
    add-option;

    fetch flg-took-key;
    jump-true no-key;
    say "\n\nA key can be seen glinting in the sun not far from the gate.";
    push "Take key";
    push take-key;
    add-option;
    label no-key;
}

NODE take-key {
    add-items 1 gnoll-cage-key;
    store flg-took-key true;
    say "You pick up the key and stick it in your pocket.";
    add-return;
}

NODE gnoll-cage {
    push "By a Cage in the Forest";
    set-location;
    say "You stand in a clearing in the forest. The area is dominated by a large, if rusty, cage.";
    push "Return to the forest path";
    push forest-path;
    add-option;

    fetch flg-rescued-gnoll;
    jump-true no-gnoll;
    say " Inside the cage you can see a rather emaciated gnoll.";
    push "Open the cage";
    push open-cage;
    add-option;
    push "Try to talk to the gnoll";
    push talk-to-gnoll;
    add-option;
    end;

    label no-gnoll;
    say " The cage stands empty and open.";
}

NODE talk-to-gnoll {
    add-return;
    fetch flg-talked-to-caged-gnoll;
    jump-true second-talk;
    store flg-talked-to-caged-gnoll true;
    say "You can barely make out the gnoll's words; \"Help me, please!\"";
    end;
    label second-talk;
    say "The gnoll continues to plead for help in a barely audible voice.";
}

NODE open-cage {
    add-return;
    item-qty gnoll-cage-key;
    jump-false no-key;
    say "Using the key you found you are able to open the cage; as you do, both the key and the lock turn to ash. As you open the door, the gnoll rushes out surprisingly fast given his state.\n\n\"Thank you!\" he says. \"Please, come see me in my home. I live to the north of the town.\"";
    remove-items 1 gnoll-cage-key;
    store flg-rescued-gnoll true;
    end;

    label no-key;
    say "You rattle the door of the cage, but it is firmly locked.";
}

NODE forest-fork {
    push "A Fork in the Path";
    set-location;
    say "The path through the forest branches here, one fork leading north, towards what appears to be a small cabin while another leads deeper into the forest. It is also possible to return to the south, towards the town.";
    push "Take the north path";
    push outside-cabin;
    add-option;
    push "Go deeper into the forest";
    push deep-forest;
    add-option;
    push "Return to the south";
    push forest-path;
    add-option;
}

NODE outside-cabin {
    push "A Cabin in the Forest";
    set-location;
    say "A small cabin has been built here, nestled between the trees.";
    fetch flg-rescued-gnoll;
    jump-false no-gnoll;
    say " A trail of smoke can be seen curling up from the chimney; it appears someone is home!";
    label no-gnoll;
    push "Re-eneter the forest";
    push forest-fork;
    add-option;
    push "Try to enter the cabin.";
    push entering-cabin;
    add-option;
}

NODE entering-cabin {
    fetch flg-rescued-gnoll;
    jump-true gnoll-home;
    say "Try as you might, the door is quite secure.";
    add-return;
    end;

    label gnoll-home;
    store flg-visited-gnoll true;
    say "You hear the voice of the gnoll call out from inside. \"Come on in!\"\n\n";
    push inside-cabin;
    do-node;
}

NODE inside-cabin {
    push "Inside the Gnoll's Cabin";
    set-location;
    say "The interior of the cabin is crampt, but cozy. You can see the gnoll slotched back on a wooden chair, a mug of some dark liquid in his hand.\n\n\"Good to see you again, my rescuer!\" he says as you enter.";
    push "Talk to the gnoll";
    push gnoll-cabin-talk;
    add-option;
    push "Leave";
    push outside-cabin;
    add-option;
}

NODE gnoll-cabin-talk {
    say "You have a nice chat with the gnoll.";
    add-return;
}

NODE deep-forest {
    push "Deep in the Forest";
    set-location;
    say "You are deep within the forest. A path leads back out towards civilization, or you can take your chances exploring the forest.";
    push "Head towards civilization";
    push forest-fork;
    add-option;
    push "Explore a while";
    push forest-do-event;
    add-option;
}

/* ************************************************************************* *
 * SOME RANDOM FOREST EVENTS                                                 *
 * ************************************************************************* */

NODE forest-do-event {
    random-from-list #forest-events;
    do-node;
}
NODE forest-event-setup {
    create-list forest-events;
    add-to-list forest-event-imps #forest-events;
    add-to-list forest-event-nothing #forest-events;
    add-to-list forest-event-skeleton #forest-events;
}

NODE forest-event-imps {
    say "You stumble across a pair of imps eating a meal. They wish you a good day and you carry on your way.";
    add-return;
}

NODE forest-event-skeleton {
    say "You discover a menacing looking skeleton loitering around a grave. It fails to notice you entirely.";
    add-return;
}

NODE forest-event-nothing {
    say "You stumble around in the forest for a while, but eventually end up back were you started without finding anything.";
    add-return;
}